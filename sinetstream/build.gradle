// From Kotlin-1.4.20, kotlin-android-extensions is deprecated.
plugins {
  id 'com.android.library'
  id 'kotlin-android'
  //id 'kotlin-android-extensions'
  id 'de.mannodermaus.android-junit5'
  id 'maven-publish'
}

android {
    compileSdk 33
    buildToolsVersion '33.0.2'

    defaultConfig {
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
        testInstrumentationRunnerArguments
        minSdk 26
        targetSdk 33
        versionCode currentCode as Integer
        versionName currentVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        jniLibs {
            excludes += ['META-INF/LICENSE*']
        }
        resources {
            excludes += ['META-INF/LICENSE*']
        }
    }

    namespace 'jp.ad.sinet.stream'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
    }
}

//
// We use the Eclipse Paho Android Service
// https://github.com/eclipse/paho.mqtt.android#gradle
//
if (properties.containsKey('gitLabPrivateToken')) {
    repositories {
        maven {
            // For PahoMqttAndroid-bugfix library
            url "https://gitlab.vcp-handson.org/api/v4/projects/126/packages/maven"
            name "GitLab"
            credentials(HttpHeaderCredentials) {
                name = 'Private-Token'
                value = gitLabPrivateToken
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
} else {
    repositories {
        maven {
            // For PahoMqttAndroid-bugfix library
            url "https://gitlab.vcp-handson.org/api/v4/projects/126/packages/maven"
            name "GitLab"
            credentials(HttpHeaderCredentials) {
                name = 'Job-Token'
                value = System.getenv('CI_JOB_TOKEN')
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

repositories {
    maven {
        url "https://repo.eclipse.org/content/repositories/paho-snapshots/"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    /*
     * WORKAROUND: Avoid using core-ktx:1.10.0 for now.
     *
     * Duplicate class XXX found in among modules:
     *   kotlin-stdlib:1.8.10
     *   kotlin-stdlib-jdk8:1.6.0
     *   kotlin-stdlib-jdk7:1.7.10
     */
    //implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'org.yaml:snakeyaml:2.0'
    implementation 'org.apache.avro:avro:1.11.1'
    implementation 'com.google.code.gson:gson:2.10.1'

    /*
     * Workaround for Android 12+
     *
     * Current implementation of the Eclipse Paho Android Service library
     * (org.eclipse.paho.android.service) is quite out-of-date, and it
     * does not work on Android 12+.
     * As a temporal solution until the library is officially fixed, link
     * the locally-fixed version (pahomqttandroid-bugfix) instead.
     */
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    //implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
    implementation 'net.sinetstream:pahomqttandroid-bugfix:1.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'

    /*
     * Android-Junit5
     * https://github.com/mannodermaus/android-junit5#instrumentation-test-support
     */
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    androidTestImplementation 'de.mannodermaus.junit5:android-test-core:1.3.0'
    androidTestRuntimeOnly 'de.mannodermaus.junit5:android-test-runner:1.3.0'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
  publishing {
    publications {
      maven(MavenPublication) {
        from components.release
        artifact androidSourcesJar
        groupId = 'jp.ad.sinet.sinetstream'
        artifactId = 'SINETStream-android'
        version = currentVersion
      }
    }
    repositories {
      maven {
        url = System.getenv('MAVEN_ENDPOINT')
        credentials(HttpHeaderCredentials) {
          name = "Job-Token"
          value = System.getenv('CI_JOB_TOKEN')
        }
        authentication {
          header(HttpHeaderAuthentication)
        }
      }
    }
  }
}
