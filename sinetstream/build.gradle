import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

// From Kotlin-1.4.20, kotlin-android-extensions is deprecated.
plugins {
  id 'com.android.library'
  id 'kotlin-android'
  //id 'kotlin-android-extensions'
  id 'de.mannodermaus.android-junit5'
  id 'maven-publish'
}

android {
    compileSdk 35

    defaultConfig {
        minSdk 26
        targetSdk 35
        versionCode currentCode as Integer
        versionName currentVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
        testInstrumentationRunnerArguments
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    packagingOptions {
        jniLibs {
            excludes += ['META-INF/LICENSE*']
        }
        resources {
            excludes += ['META-INF/LICENSE*']
        }
    }

    //
    // We need to configure Android publication 'maven' for variant 'release'.
    // https://developer.android.com/build/publish-library/configure-pub-variants
    //
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    namespace 'jp.ad.sinet.stream'
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
}

//
// We use the Eclipse Paho Android Service
// https://github.com/eclipse/paho.mqtt.android#gradle
//
if (properties.containsKey('gitLabPrivateToken')) {
    repositories {
        maven {
            // For PahoMqttAndroid-bugfix library
            url "https://gitlab.vcp-handson.org/api/v4/projects/126/packages/maven"
            name "GitLab"
            credentials(HttpHeaderCredentials) {
                name = 'Private-Token'
                value = gitLabPrivateToken
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
} else {
    repositories {
        maven {
            // For PahoMqttAndroid-bugfix library
            url "https://gitlab.vcp-handson.org/api/v4/projects/126/packages/maven"
            name "GitLab"
            credentials(HttpHeaderCredentials) {
                name = 'Job-Token'
                value = System.getenv('CI_JOB_TOKEN')
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

repositories {
    maven {
        url "https://repo.eclipse.org/content/repositories/paho-snapshots/"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.1.20'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'org.yaml:snakeyaml:2.4'
    implementation 'org.apache.avro:avro:1.12.0'
    implementation 'com.google.code.gson:gson:2.12.1'

    /*
     * Workaround for Android 12+
     *
     * Current implementation of the Eclipse Paho Android Service library
     * (org.eclipse.paho.android.service) is quite out-of-date, and it
     * does not work on Android 12+.
     * As a temporal solution until the library is officially fixed, link
     * the locally-fixed version (pahomqttandroid-bugfix) instead.
     */
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    //implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
    implementation 'net.sinetstream:pahomqttandroid-bugfix:1.9.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.12.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.12.1'

    /*
     * Android-Junit5
     * https://github.com/mannodermaus/android-junit5#instrumentation-test-support
     */
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.1'
    androidTestImplementation 'de.mannodermaus.junit5:android-test-core:1.7.0'
    androidTestRuntimeOnly 'de.mannodermaus.junit5:android-test-runner:1.7.0'
}

afterEvaluate {
  publishing {
    publications {
      maven(MavenPublication) {
        from components.release
        groupId = 'jp.ad.sinet.sinetstream'
        artifactId = 'SINETStream-android'
        version = currentVersion
      }
    }
    repositories {
      maven {
        url = System.getenv('MAVEN_ENDPOINT')
        credentials(HttpHeaderCredentials) {
          name = "Job-Token"
          value = System.getenv('CI_JOB_TOKEN')
        }
        authentication {
          header(HttpHeaderAuthentication)
        }
      }
    }
  }
}
